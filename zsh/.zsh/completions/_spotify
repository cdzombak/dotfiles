#compdef spotify

local curcontext="$curcontext" state line
typeset -A opt_args

_arguments -C \
  ':command:->command' \
  '*::options:->options'

case $state in
  (command)
    local -a subcommands
    subcommands=(
      'play:Resume playback, play a given song by name, or play album/artist/playlist via subcommands'
      'pause:Pause playback'
      'next:Skip to the next song in a playlist'
      'prev:Return to the previous song in a playlist'
      'pos:Jump to given time (in secs) in the current song'
      'quit:Stop playback and quit Spotify'
      'vol:Set volume to a given amount from 0 to 100, or up/down/show via subcommands'
      'status:Show the current player status'
      'share:Copy the current song URL to the clipboard'
      'info:Show full information about the currently-playing song'
      'toggle:Toggle shuffle or repeat playback mode'
    )
    _describe -t commands 'spotify' subcommands
    _arguments \
			-h'[Display help]'\
			--help'[Display help]'
  ;;

  (options)
    case $line[1] in

      (play)
        local -a play_subcommands
        play_subcommands=(
          'album:Find an album by name and play it'
          'artist:Find an artist by name and play it'
          'list:Find a playlist by name and play it'
        )
        _describe -t commands 'spotify play' play_subcommands
      ;;

      (vol)
        local -a vol_subcommands
        vol_subcommands=(
          'up:Increase the volume by 10%'
          'down:Decrease the volume by 10%'
          'show:Show the current Spotify volume'
        )
        _describe -t commands 'spotify vol' vol_subcommands
      ;;

      (toggle)
        local -a toggle_subcommands
        toggle_subcommands=(
          'shuffle:Toggle shuffle playback mode'
          'repeat:Toggle repeat playback mode'
        )
        _describe -t commands 'spotify toggle' toggle_subcommands
      ;;

    esac
  ;;
esac
